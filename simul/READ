*\       ---- SAVE:LOAD ----               08.09.86 VERSION 92
**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=**
**   WORKING WTIH VIRTUAL ARRAY  **
**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=**
**
LOCALS *LLOAD

  DATA TYPOPR
  DATA MEMADR,BYTE            % AДPEC БAЙTA И COДEPЖИMOE
  DATA MEMIND=:M:TAB          % TAБЛИЧKA C ИHДEKCAMИ БУФEPOB
  DATA BUFTAB=:B:TAB          %        - C AДPECOM БУФEPA
  DATA CONTAB=:C:TAB          %        - C CЧETЧИKOM
  DATA CONTB2=:C:TAB+2
  DATA PAGTAB=:P:TAB          %        - C HOMEPOM CTPAHИЦЫ
  DATA MODTAB=:F:TAB          %        - ФЛAЖKИ ИЗMEHEHИЯ

  DATA XWORK,WORK             % PAБOЧИE ЯЧEЙKИ (ДBA CЛOBA)
  DATA ASWAP=SWAPIN,RSWAP     % ДЛЯ ПOДПPOГPAMMЫ

  DATA 'NBUF'=16              % ЧИCЛO БУФEPOB
  DATA INICON=16*4            % HAЧAЛЬHOE ЗHAЧEHИE CЧETЧИKA

  DATA :B:TAB=0,(NBUF)*=%*&100+!MBUF
  DATA :C:TAB[NBUF+1]
  DATA :P:TAB=0,(NBUF)*=&FFFF
  DATA :F:TAB[NBUF+1]

  BYTES :M:TAB[256]           % TAБЛИЧKA ИHДEKCOB (CHAЧAЛA C HУЛЯMИ)
  BUFFERS MBUF(256*NBUF)

*\
**     ЗAПИCЬ БAЙTA B BИPTУAЛЬHЫЙ MACCИB
**           --> ^A    -- AДPEC
**               ^E    -- TИП OПEPAЦИИ   =0 - CЛOBO   <>0 - БAЙT
**               ^X    -- CЛOBO (TИП=0)   ИЛИ   БAЙT ! ГPЯЗЬ (TИП<>0)
**           <-- CARRY -- TRUE  - HOPMAЛЬHOE OKOHЧAHИE
**                     -- FALSE - OШИБKA OБPAЩEHИЯ K KAHAЛУ
**            ^A,^E,^X -- ИCПOPЧEHЫ
**
PROGRAM WRITE(MEMADR,,BYTE)
  IF >1= AND ^E= THEN RETURN ,,2
  (IF ,,MEMIND(MEMADR:):=
*        ИCПOPЧEHO ФЛEPOBЫM
    IF MEMADR:>=&E0 THEN CALL DEVWR MEMADR,,BYTE ; RETURN ,
    ,TYPOPR=
    GOSUB @ASWAP(RSWAP)
    =,TYPOPR
  IF)
  WORK=BUFTAB()
  $CONTAB()=MODTAB()=1
  =,,(MEMADR+1):
  IF ^E= THEN WORK()=BYTE.XAA. ELSE WORK():=BYTE:
  RETURN ,,0


*\
**  ЗAГPУЗKA БAЙTA ИЗ BИPTУAЛЬHOГO MACCИBA
**           --> ^A    -- AДPEC БAЙTA
**               ^E    -- TИП OПEPAЦИИ   =0 - CЛOBO   <>0 - БAЙT
**           <-- CARRY -- TRUE  - HOPMAЛЬHOE OKOHЧAHИE
**                     -- FALSE - OШИБKA OБPAЩEHИЯ K KAHAЛУ
**               ^A    -- CЛOBO (TИП=0)   ИЛИ   БAЙT ! БAЙT (TИП<>0)
**               ^E,^X -- ИCПOPЧEHЫ
**
PROGRAM READ(MEMADR)
  IF >1= AND ^E= THEN RETURN ,,2
  (IF ,,MEMIND(MEMADR:):=
*    ИCПOPЧEHO ФЛEPOBЫM
    IF MEMADR:>=&E0 THEN CALL DEVRD MEMADR ; RETURN ,
    ,TYPOPR=
    GOSUB @ASWAP(RSWAP)
    =,TYPOPR
  IF)
  WORK=BUFTAB()
  $CONTAB()=1
  =,,(MEMADR+1):
  IF ^E= THEN =WORK().XAA. ELSE =WORK()[WORK():
  RETURN ,,0


*\
**   OБMEH C ДИCKOM, ECЛИ HEOБXOДИMOГO БУФEPA HET B ПAMЯTИ.
**
*SUBROUTINE SWAPIN
    <<SWAPIN>> =
*   ......ИЩEM БУФEP C MИHИMAЛЬHЫM ЧИCЛOM OБPAЩEHИЙ
  WORK=#MIN1
  (XCYCLE 'NBUF*2' STEP 2) IF CONTB2()-WORK<> THEN WORK,,XWORK=CONTB2()
* ......MEHЯEM CTPAHИЦУ B БУФEPE
  ,,XWORK=BUFTAB(XWORK+2)
  IF ,MODTAB()<> THEN CALL M:SAVE ,,PAGTAB()
  CALL M:LOAD ,,MEMADR:
* ......MEHЯEM CЧETЧИKИ
  (IF PAGTAB(XWORK)>=
    (XCYCLE 'NBUF*2' STEP 2) $CONTB2()=CONTB2().RLS.2.CNA.
* ......MOДИФИЦИPУEM KAPTУ БУФEPOB
    MEMIND(PAGTAB(XWORK)):=0
  IF)
  MEMIND(MEMADR:):=XWORK
* ......ЗAПOЛHЯEM ЭЛEMEHTЫ TAБЛИЦ ДЛЯ HOBOЙ CTPAHИЦЫ
  PAGTAB(XWORK)=MEMADR:
  MODTAB(),CONTAB()=0,INICON
* ......OCTABЛЯEM B <X> HOMEP (УДBOEHHЫЙ) PAБOЧEГO БУФEPA
 GOTO @RSWAP

*\
**   OБMEH C ДИCKOM
**
LOCALS -LMSAVE
  DATA [2]
  USE ACR:STRUCT,FIL:STRUCT,LIB:PARAMS
  DATA XLIBR=LBLIBR+&100
  DATA 'READ'=0,'WRITE'=1
  DATA 'MAPSZ'=256,MAP(MAPSZ)
  DATA 'LBUF'=256
  DATA MAXEC=-1,PAGE,'FREE'=0
  DATA IOB=WRITE,ABUF,LENG=LBUF,NSEC=0
  DATA 'COMAND'=IOB+1

*\  ЗAПИCЬ CEKTOPA HA ДИCK
**         ^A -- AДPEC БУФEPA
**         ^X -- HOMEP CEKTOPA
**
PROGRAM M:SAVE(ABUF,,PAGE)
  (IF MAXEC=#MIN1
    MAXEC=0
    MAP('MAPSZ-1'):='FREE'
    !MAP(0)=MAP+1.XEX.
    DATA CREAT=(1.0,0."-"."!64K:BYTES")
    !#AZC(0)=CREAT,20
    IF CSV START 'LIBCRE',,XLIBR<0 THEN STOP ,
    IOB:=#AZC(1):
    CALL :IO IOB\
  IF)
  NSEC=((IF PAGE<> AND MAP(PAGE):='FREE' THEN MAP():=$MAXEC=1))
  COMAND:='WRITE'
  CALL :IO IOB\
  RETURN ABUF,,PAGE

*\  ЧTEHИE CEKTOPA C ДИCKA
**         ^A -- AДPEC БУФEPA
**         ^X -- HOMEP CEKTOPA
**
**
PROGRAM M:LOAD(ABUF,,PAGE)
  (IF MAXEC=#MIN1 OR (,,PAGE<> AND MAP():='FREE')
    ABUF(LENG-1):=&00
    !ABUF(0)=ABUF+1.XEX.
  ELSE
    NSEC=MAP(PAGE):
    COMAND:='READ'
    CALL :IO IOB\
  IF)
  RETURN ABUF,,PAGE

