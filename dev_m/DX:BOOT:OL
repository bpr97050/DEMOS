%% =============================== %% 28.10.87 VERSION 51
%% == ЗАГРУЗЧИК С ФЛОППИ-ДИСПКА == %%
%% =============================== %%
%- ИСПОРЧЕН НЕАВТОРСКОЙ ПРАВКОЙ:
%-  ДОБАВЛЕН NOP
%-  ЯЧЕЙКИ &0C,&0E,&10 ЗАНЯТЫ ПАРАМЕТРАМИ, ПРОСТАВЛЯЕМЫМИ SYSGEN'ОМ
%-  ПРЕДПОЛАГАЕТСЯ, ЧТО МОНИТОР НАЧИНАЕТСЯ НА 1-Й ДОРОЖКЕ
GLOBAL (START=&00) (LOCATION=&00)
%%
%% РЕГИСТРЫ FLOPPY, БИТЫ КОМАНД
%%
  EQUALS FKS=&FE78,FKD=&FE7A
  EQUALS READ=&6,RBUFF=&2,GO=&1,UNIT=&10

  EQUALS TPS=&FF74,TPB=&FF76

LOCALS LSTART
%- ПЕРВОЙ КОМАНДОЙ ДОЛЖЕН БЫТЬ NOP
. CCL
. BR   START
  DATA =IT04.&E0
%-- ИНТЕРФЕЙСНАЯ ТАБЛИЦА -------%
  DATA DXLENG                   %       %ЗНАЧЕНИЕ ПРОСТАВЛЯЕТ SYSGEN
%-------------------------------%
  BYTES SELTAB=READ+GO,READ+GO+UNIT
  DATA =IT0C.&E0                        %BPT-IT

  REGISTERS BUFADR=%1,SECTOR=%2,TRACK=%3,ASTATE=%4,ADATA=%5

  EQUALS ASTRTR=&02

PROGRAM START()
  RDOPER:=SELTAB[R0]                    %ПРОСТАВЛЯЕМ НОМЕР УСТРОЙСТВА

  TRACK=1                               %С ПЕРВОЙ ДОРОЖКИ
  SECTOR='ASTRTR*4'                     %ПЕРВЫЙ БЛОК ПОСЛЕ МЕТКИ ТОМА
  SP=BUFADR(&400)                       %АДР. ЗАГРУЗКИ И ЗАПУСКА
  _SP=DXLENG                            %ЧИСЛО БЛОКОВ

  ASTATE='FKS' ; ADATA='FKD'
  (DCYCLE(@SP) .ASL..ASL.
    SECTOR=+1
    @ASTATE=^PC
.RDOPER DAT 0                           *КОМАНДА ЧТЕНИЯ + НОМЕР УСТР.
.   BPT
    @ADATA=SECTOR                       %НОМЕР СЕКТОРА
.   BPT
    @ADATA=TRACK                        %НОМЕР ДОРОЖКИ
.   BPT
    @'TPB'="+"
    @ASTATE='RBUFF+GO'                  %ЧТЕНИЕ ИЗ БУФЕРА
    (WHILE
.     BPT
    WHILE @ASTATE:<0
      ^BUFADR:=@ADATA
    )WHILE
    (IF SECTOR=26
      SECTOR=0 ; TRACK=+1
      @'TPB'="/"
    )IF
  )DCYCLE
. TST (%6)+
  PC=SP

LABEL IT0C                              % IOT: ОЖИДАНИЕ "IT"
  REPEAT @ASTATE=.BIT.&80A0 WHILE IS ZERO
  (IF IS NEGATIVE                       %БЫЛА ОШИБКА ?
LABEL IT04
    HALT
  )IF
  DESACTIVATE
